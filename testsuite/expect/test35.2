#!/usr/bin/env expect
############################################################################
# Purpose: Test of Slurm functionality
#          Cray job-specific burst buffer use
############################################################################
# Copyright (C) 2015-2018 SchedMD LLC.
# Written by Morris Jette <jette(at)schedmd.com>
#
# This file is part of Slurm, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# Slurm is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# Slurm is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with Slurm; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set data_in     "DATA_IN_0123456789"
set data_out    "DATA_OUT_9876543210"
set exit_code   0
set input_data  "test$test_id.data.in"
set output_data "test$test_id.data.out"
set output_use  "test$test_id.use.output"
set script_use  "test$test_id.use.bash"
set tmp_file    "test$test_id"

proc find_bb_jobid { fname bb_jobid } {
	global bin_cat

	set found 0
	log_user 0
	spawn $bin_cat $fname
	expect {
		-re "JobID=$bb_jobid" {
			set found 1
			exp_continue
		}
		eof {
			wait
		}
	}
	log_user 1
	return $found
}

proc find_bb_data { file_name string_seek } {
	global bin_cat

	set found 0
	spawn $bin_cat $file_name
	expect {
		-re $string_seek {
			set found 1
			exp_continue
		}
		eof {
			wait
		}
	}
	return $found
}

stop_root_user

set bb_types [get_bb_types]
if {[string first "datawarp" $bb_types] == -1} {
	skip "This test can only be run with the burst_buffer/datawarp plugin"
}

set cwd [$bin_pwd]
exec $bin_rm -f $input_data $output_data $output_use
exec $bin_echo $data_in >$cwd/$input_data

if {[get_bb_emulate] != 0} {
make_bash_script $script_use "#DW jobdw type=scratch capacity=1GiB access_mode=striped,private type=scratch
#DW stage_in type=file source=$cwd/$input_data destination=\$DW_JOB_STRIPED/$tmp_file
#DW stage_out type=file destination=$cwd/$output_data source=\$DW_JOB_STRIPED/$tmp_file
$bin_cat  $cwd/$input_data
$bin_echo $data_out >$cwd/$output_data
$scontrol show burst
exit 0"
} else {
make_bash_script $script_use "#DW jobdw type=scratch capacity=1GiB access_mode=striped,private type=scratch
#DW stage_in type=file source=$cwd/$input_data destination=\$DW_JOB_STRIPED/$tmp_file
#DW stage_out type=file destination=$cwd/$output_data source=\$DW_JOB_STRIPED/$tmp_file
$bin_cat  \$DW_JOB_STRIPED/$tmp_file
$bin_echo $data_out >\$DW_JOB_STRIPED/$tmp_file
$scontrol show burst
exit 0"
}

incr max_job_state_delay 300
set job_id_use 0
spawn $sbatch -t1 -N1 --output=$output_use $script_use
expect {
	-re "Submitted batch job ($number)" {
		set job_id_use $expect_out(1,string)
		exp_continue
	}
	timeout {
		log_error "sbatch not responding"
		set exit_code 1
	}
	eof {
		wait
	}
}

if {$job_id_use == 0} {
	log_error "Job did not launch"
	set exit_code 1
} elseif {[wait_for_job $job_id_use "DONE"] != 0} {
	log_error "Job did not complete"
	set exit_code 1
	cancel_job $job_id_use
} elseif {[wait_for_file $output_use] != 0} {
	set exit_code 1
} elseif {[find_bb_jobid $output_use $job_id_use] == 0} {
	log_error "Job burst buffer not found"
	set exit_code 1
} elseif {[find_bb_data $output_use $data_in] == 0} {
	log_error "Job burst buffer input data not found"
	set exit_code 1
} elseif {[find_bb_data $output_data $data_out] == 0} {
	log_error "Job burst buffer output data not found"
	set exit_code 1
}

# Wait for purge of buffer to complete
sleep 10

set found 0
spawn $scontrol show burst
expect {
	-re "JobID=$job_id_use" {
		set found 1
		exp_continue
	}
	timeout {
		log_error "scontrol not responding"
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$found != 0} {
	fail "Job burst buffer not removed"
}

if {[find_bb_data $output_data $data_out] == 0} {
	fail "Job output burst buffer data not correct"
}

if {$exit_code == 0} {
	exec $bin_rm -f $input_data $output_data
	exec $bin_rm -f $output_use $script_use
} else {
	fail "Test failed due to previous errors (\$exit_code = $exit_code)"
}
