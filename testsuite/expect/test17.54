#!/usr/bin/env expect
############################################################################
# Purpose: Test of Slurm functionality
#          Test --deadline to be used as the timelimit.
#
#          At the moment this test only really works if the DefaultTime is
#          longer than 1 hour to verify the timelimit is set accordingly.
############################################################################
# Copyright (C) 2015 CEA/DAM/DIF
# Written by Aline Roy <aline.roy@cea.fr>
#
# This file is part of Slurm, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# Slurm is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# Slurm is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with Slurm; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set file_in     "test$test_id.input"
set exit_code   0
set job_id      0

#
# We must be SlurmUser or root in order to change the partition MaxTime limit,
# otherwise this test may fail.
#
if {![is_super_user]} {
	skip "You must be SlurmUser or root to run this test"
}

proc cleanup {} {
	global bin_rm file_in

	# Restore partition MaxTime and DefaultTime.
	reconfigure
	exec $bin_rm -f $file_in
}

set default_part [default_partition]
if { $default_part == "" } {
	fail "Unable to get the default partition"
}

#
# Ensure that MaxTime is UNLIMITED so this test won't fail due to trying to set
# time limits greater than MaxTime. Ensure DefaultTime is UNLIMITED so the job
# time limit is what we expect.
#
spawn $scontrol update partitionname=$default_part MaxTime=-1 DefaultTime=-1
expect {
	timeout {
		fail "scontrol not responding"
	}
	eof {
		wait
	}
}
set timelimit [get_partition_maximum_time_limit $default_part]
if { $timelimit != -1 } {
	fail "Unable to update partition MaxTime to UNLIMITED"
}
set timelimit [get_partition_default_time_limit $default_part]
if { $timelimit != -1 } {
	fail "Unable to update partition DefaultTime to UNLIMITED"
}

#
# Build input script file
#
exec $bin_rm -f $file_in
make_bash_script $file_in "$bin_sleep 1"

#
# Spawn a batch job that deadline is too late
#
set timeout $max_job_delay
spawn $sbatch --output=/dev/null --error=/dev/null --deadline now+1hour $file_in
expect {
	-re "Requested time limit is invalid" {
		set exit_code 1
		exp_continue
	}
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		fail "sbatch not responding"
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	fail "Job submission failed"
}

if {[wait_for_job $job_id DONE] != 0} {
	fail "Job failed to run to completion"
}

#
# verify time limit
#
spawn $scontrol show job $job_id
expect {
	-re "TimeLimit=00:59:" {
		exp_continue
	}
	-re "TimeLimit=01:00:00" {
		exp_continue
	}
	-re "TimeLimit=" {
		log_error "Time limit is different from deadline"
		set exit_code 1
		exp_continue
	}
	timeout {
		fail "scontrol not responding"
	}
	eof {
		wait
	}
}

if {$exit_code != 0} {
	fail "Test failed due to previous errors (\$exit_code = $exit_code)"
}
