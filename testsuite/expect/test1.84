#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Test of cpus-per-task option on a single node (--cpus-per-task  
#          option).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "WARNING: ..." with an explanation of why the test can't be made, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
# NOTE:    This assumes node names are of the form <alpha><number>, where 
#          the value of <number> indicates the nodes relative location. 
#          Change tha node name parsing logic as needed for other formats.
############################################################################
# Copyright (C) 2002-2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# UCRL-CODE-217948.
# 
# This file is part of SLURM, a resource management program.
# For details, see <http://www.llnl.gov/linux/slurm/>.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set test_id     "1.84"
set exit_code   0

print_header $test_id

if { [test_front_end] } {
	send_user "\nWARNING: This test is incompatable with FRONT_END systems\n"
	exit 0
}

#
# Submit a 1 node job to determine the node's CPU count
#
set cpu_count      0
set fat_nodes      0
set host           ""
set timeout        $max_job_delay
set node_cnt       1
set task_cnt       1

spawn $srun -N$node_cnt --cpus-per-task=$task_cnt -l -t1 $bin_hostname  
expect {
	-re "($number): ($alpha_numeric)" {
		set task $expect_out(1,string)
		set host $expect_out(2,string)
		if {$task >= $cpu_count} {
			set cpu_count [expr $task + 1]
		}
		exp_continue
	}
	-re "Task count specification invalid" {
		send_user "\nWARNING: nodes have too many CPUs for test\n"
		set fat_nodes 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}
# On Blue Gene/L each node has 1024 CPUs, more than slurmd can launch
if {$fat_nodes == 1} {
	exit 0
}

#
# Check CPU count
#
if {[string compare $host ""] == 0} {
	send_user "\nFAILURE: Did not get hostname\n"
	exit 1	
}
if {$cpu_count < 2} {
	send_user "\nWARNING: The node only has one CPU\n"
	exit $exit_code
}

#
# Submit a 1 node job to determine the node's CPU count
#
set read_cpus 0
spawn $srun -N1 --cpus-per-task=2 --nodelist=$host -l -t1 $bin_hostname  
expect {
	-re "$number: ($alpha_numeric)" {
		incr read_cpus
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}

#
# Check CPU count
#
if {$read_cpus != [expr $cpu_count / 2]} {
	send_user "\nFAILURE: Improper task count for given cpus-per-task\n"
	set exit_code   1	
}


if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code
