#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Basic UPC (Unified Parallel C) test via srun.
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "WARNING: ..." with an explanation of why the test can't be made, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2012 SchedMD LLC.
# Written by Morris Jette <jette@schedmd.com>
#
# This file is part of SLURM, a resource management program.
# For details, see <http://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set test_id            "1.95"
set exit_code          0
set file_in            "test$test_id.input"
set file_out           "test$test_id.output"
set file_err           "test$test_id.error"
set test_prog          "test$test_id.prog"
set job_id             0
set task_cnt           8

print_header $test_id

#
# Test for existence of UPC compiler
#
if {[info exists upcc] == 0} {
	send_user "\nWARNING: UPC compiler (upcc) not defined, can't perform UPC testing\n"
	exit 0
}
if {[file executable $upcc] == 0} {
	send_user "\nWARNING: $upcc does not exists\n"
	exit 0
}
if {[test_front_end]} {
	send_user "\nWARNING: This test is incompatible with front-end systems\n"
	exit 0
} elseif {[test_aix]} {
	send_user "WARNING: Test is incompatible with AIX\n"
	exit 0
} elseif {[test_serial]} {
	send_user "WARNING: Test is incompatible with serial system\n"
	exit 0
}

#
# Delete left-over program and rebuild it
#
exec $bin_rm -f $test_prog ${test_prog}.o
exec $upcc -o $test_prog -qupc=threads=$task_cnt ${test_prog}.upc

# Delete left-over stdout/err files
file delete $file_out $file_err

#
# Build input script file
#
make_bash_script $file_in "
  $srun -n $task_cnt ./$test_prog
"

#
# Spawn an sbatch job that uses stdout/err and confirm their contents
#
set timeout $max_job_delay
set no_start 0
set sbatch_pid [spawn $sbatch -N1-$task_cnt -n $task_cnt --output=$file_out --error=$file_err -t1 $file_in]
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "Batch job submission failed" {
		set no_start 1
		exp_continue
	}
	-re "Unable to contact" {
		send_user "\nFAILURE: slurm appears to be down\n"
		exit 1
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		slow_kill $sbatch_pid
		set exit_code 1
	}
	eof {
		wait
	}
}

if {$no_start != 0} {
	send_user "\nWARNING: partition too small for test\n"
	if {$job_id != 0} {
		cancel_job $job_id
	}
	exit 0
}
if {$job_id == 0} {
	send_user "\nFAILURE: batch submit failure\n"
	exit 1
}

#
# Wait for job to complete
#
if {[wait_for_job $job_id "DONE"] != 0} {
	send_user "\nFAILURE: waiting for job to complete\n"
	set exit_code 1
}

#
# Check for desired output in stdout
#
if {[wait_for_file $file_out] == 0} {
	set matches  0 
	set sum      0
	set total    0
	spawn $bin_cat $file_out
	expect {
		-re "Hello from ($number) of $task_cnt" {
			incr matches
			incr sum $expect_out(1,string)
			exp_continue
		}
		-re "Total is ($number)" {
			set total $expect_out(1,string)
			exp_continue
		}
		-re "Failed to mmap" {
			send_user "\nFAILURE: insufficient shared memory. "
			send_user "Rebuild UPCC with --disable-pshm option.\n"
			exp_continue
		}
		eof {
			wait
		}
	}
	if {$matches == 0} {
		send_user "\nFAILURE: No UPC communications occurred\n"
		set exit_code 1
	} elseif {$matches != $task_cnt} {
		send_user "\nFAILURE: unexpected output ($matches of $task_cnt)\n"
		set exit_code 1
	} elseif {$sum != $total} {
		send_user "\nFAILURE: Problem with global variables ($sum != $total)\n"
		set exit_code 1
	}
} else {
	set exit_code 1
}

if {$exit_code == 0} {
	exec $bin_rm -f $file_in $file_out $file_err $test_prog ${test_prog}.o
	send_user "\nSUCCESS\n"
} else {
	send_user "Check contents of $file_err\n"
}

exit $exit_code
