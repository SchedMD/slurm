#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Confirm that a job executes with the proper task count (--ntasks
#          option).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2002-2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
#
# This file is part of SLURM, a resource management program.
# For details, see <https://computing.llnl.gov/linux/slurm/>.
# Please also read the included file: DISCLAIMER.
#
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set test_id     "17.6"
set exit_code   0
set file_in     "test$test_id.input"
set file_out    "test$test_id.output"
set task_cnt    2
set tasks       0

print_header $test_id

#
# Submit a slurm job that will execute 'id' on $task_cnt tasks (or try anyway)
#
file delete $file_in $file_out
make_bash_script $file_in "$srun $bin_id"
set job_id 0
set no_run 0
spawn $sbatch --ntasks=$task_cnt --output=$file_out -t1 $file_in
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	-re "Batch job submission failed" {
		send_user "\nNo worries, error not unexpected\n"
		set no_run 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id != 0} {
	spawn $scontrol show job $job_id
	expect {
		-re "NumCPUs=($number)" {
			set tasks $expect_out(1,string)
			exp_continue
		}
		timeout {
			send_user "\nFAILURE: scontrol not responding\n"
			set exit_code 1
		}
		eof {
			wait
		}
	}
	if {$tasks < $task_cnt} {
		send_user "\nFAILURE: sbatch --tasks not satsified\n"
		set exit_code 1
	}
	if {[wait_for_job $job_id "DONE"] != 0} {
		send_user "\nFAILURE: waiting for job to complete\n"
		cancel_job $job_id
		set exit_code 1
	}
	if {[wait_for_file $file_out] == 0} {
		file delete $file_out
	} else {
		set exit_code 1
	}
} else {
	if {$no_run == 0} {
		send_user "\nFAILURE: sbatch failed to create job\n"
		set exit_code 1
	}
}
if {$exit_code != 0} {
	exit $exit_code
}

#
# Submit a slurm job that will execute 'id' on 1 node and over task_cnt tasks
#
file delete $file_in $file_out
make_bash_script $file_in "$srun -n $task_cnt --overcommit $bin_id"
set job_id 0
set tasks  0
spawn $sbatch --ntasks=1 --output=$file_out -t1 $file_in
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}
if { $job_id == 0 } {
	send_user "\nFAILURE: failed to submit job\n"
	exit 1
}

#
# Wait for job to complete
#
if {[wait_for_job $job_id "DONE"] != 0} {
	send_user "\nFAILURE: waiting for job to complete\n"
	set exit_code 1
}
if {[wait_for_file $file_out] == 0} {
	spawn $bin_cat $file_out
	expect {
		-re "uid=" {
			incr tasks
			exp_continue
		}
		eof {
			wait
		}
	}
}
if {$task_cnt != $tasks} {
	send_user "\nFAILURE: Did not get proper number of tasks: "
	send_user "$task_cnt, $tasks\n"
	set exit_code 1
}

if {$exit_code == 0} {
	file delete $file_in $file_out
	send_user "\nSUCCESS\n"
}
exit $exit_code
