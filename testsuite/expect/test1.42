#!/usr/bin/env expect
############################################################################
# Purpose: Test of Slurm functionality
#          Test of job dependencies (--dependency option).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2004-2007 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
#
# This file is part of Slurm, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# Slurm is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# Slurm is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with Slurm; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set test_id     "1.42"
set file_in1    "test$test_id.input1"
set exit_code   0
set job_id1     0
set job_id2     0

print_header $test_id

# if scontrol could be ran from the compute nodes this would work as well
if {[test_alps]} {
	send_user "\nWARNING: This test is incompatible with Cray systems\n"
	exit $exit_code
}

#
# Build input script files
#
exec $bin_rm -f $file_in1
make_bash_script $file_in1 "$bin_sleep 10"

#
# Spawn a batch job that just sleeps for a while
#
set timeout $max_job_delay
set sbatch_pid [spawn $sbatch --output=/dev/null --error=/dev/null -t1 $file_in1]
expect {
	-re "Submitted batch job ($number)" {
		set job_id1 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		slow_kill $sbatch_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id1 == 0} {
	send_user "\nFAILURE: batch submit failure\n"
	exit 1
}

#
# Submit a dependent job
#
set match_job_state 0
set srun_pid [spawn $srun -t1 --dependency=afterany:$job_id1 $scontrol show job $job_id1]
expect {
	-re "JobState=COMPLETED|COMPLETING" {
		set match_job_state 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		slow_kill $srun_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$match_job_state == 0} {
	send_user "\nFAILURE: Dependent job not completed\n"
	set exit_code 1
}

if {$exit_code == 0} {
	exec $bin_rm -f $file_in1
	send_user "\nSUCCESS\n"
}
exit $exit_code
