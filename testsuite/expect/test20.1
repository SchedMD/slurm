#!/usr/bin/env expect
############################################################################
# Purpose: Test of Slurm functionality
#          qsub command tests
############################################################################
# Copyright (C) 2007 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
#
# This file is part of Slurm, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# Slurm is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# Slurm is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with Slurm; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set exit_code   0
set file_err	"test$test_id.error"
set file_in	"test$test_id.input"
set file_out	"test$test_id.output"

proc cleanup {} {
	global bin_rm file_in file_out file_err

	exec $bin_rm -f $file_in $file_out $file_err
}

if {[file executable $qsub] == 0} {
	skip "$qsub not found"
}

set job_id 0
exec rm -f $file_out $file_err
make_bash_script $file_in "$bin_echo HELLO; $bin_cat /dummy_file"
spawn $qsub -l walltime=1:00 -e $file_err -o $file_out $file_in
expect {
	-re "($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		fail "qsub not responding"
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	fail "qsub failed to submit job"
}

if {[wait_for_file $file_out] != 0} {
	catch {exec $qdel $job_id}
	fail "qsub output file not found"
}
set matches 0
spawn $bin_cat $file_out
expect {
	-re "HELLO" {
		incr matches
		exp_continue
	}
	eof {
		wait
	}
}
if {$matches != 1} {
	log_error "Unexpected qsub output file contents"
	set exit_code 1
}

if {[wait_for_file $file_err] != 0} {
	catch {exec $qdel $job_id}
	fail "qsub error file not found"
}
set matches 0
spawn $bin_cat $file_err
expect {
	-re "No such file" {
		log_debug "Message from cat expected, no worries"
		incr matches
		exp_continue
	}
	eof {
		wait
	}
}
if {$matches != 1} {
	fail "Unexpected qsub error file contents"
}

if {$exit_code != 0} {
	fail "Test failed due to previous errors (\$exit_code = $exit_code)"
}
