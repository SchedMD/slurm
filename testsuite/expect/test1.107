#!/usr/bin/env expect
############################################################################
# Purpose: Test of Slurm functionality
#          Test of --deadline option and time_min and time too long
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2015 CEA/DAM/DIF
# Written by Aline Roy <aline.roy@cea.fr>
#
# This file is part of Slurm, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# Slurm is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# Slurm is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with Slurm; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set test_id     "1.107"
set exit_code   0
set job_id      0

print_header $test_id


#
# Since we make changes to configuration, call this function restore
# configuration before exiting.
#
proc endit { } {
	global exit_code
	reconfigure
	if {$exit_code == 0} {
		send_user "\nSUCCESS\n"
	} else {
		send_user "\nFAILURE: exiting with exit code $exit_code\n"
	}
	exit $exit_code
}

if {[test_alps]} {
	send_user "\nWARNING: This test is incompatible with Cray/alps systems\n"
	exit $exit_code
}

#
# We must be SlurmUser or root in order to change the partition MaxTime limit,
# otherwise this test may fail.
#
if {[test_super_user] == 0} {
	log_warn "You must be SlurmUser or root to run this test."
	exit 0
}

#
# Ensure that MaxTime is UNLIMITED so this test won't fail due to trying to set
# time limits greater than MaxTime.
#
set exit_code [set_partition_maximum_time_limit "" -1]
if { $exit_code != 0 } {
	endit
}

#
# Spawn a job with a deadline and no time
#
set timeout $max_job_delay
set srun_pid [spawn $srun -v  -N1 --deadline now+1hour --time-min 20 -t120 $bin_sleep 1]
expect {
	-re "Requested time limit is invalid" {
		send_user "\nFAILURE: time limit error\n"
		set exit_code 1
		exp_continue
	}
	-re "launching ($number).0 on host" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: job not submitted\n"
	send_user "FAILURE: This can be due to QOS or account time limit <120 minutes\n"
	set exit_code 1
	endit
}

#
# verify time limit
#
spawn $scontrol show job $job_id
expect {
	-re "TimeLimit=01:00:00" {
		exp_continue
	}
	-re "TimeLimit=00:59:" {
		exp_continue
	}
	-re "TimeLimit=" {
		send_user "\nFAILURE: time limit is different from deadline\n"
		set exit_code 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scontrol not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}

set rc [cancel_job $job_id]
if { $rc != 0 } {
	log_error "Unable to cancel job $job_id."
	incr exit_code
}

endit
