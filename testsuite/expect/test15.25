#!/usr/bin/env expect
############################################################################
# Purpose: Test of SLURM functionality
#          Test of job account(--account option).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2004-2007 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
#
# This file is part of SLURM, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set test_id     "15.25"
set file_in     "test$test_id.input"
set exit_code   0
set job_id1     0
set job_id2     0
set job_acct    "TEST_ACCT"
set job_acct_lc "test_acct"

print_header $test_id

# if scontrol could be ran from the compute nodes this would work as well
if {[test_alps]} {
	send_user "\nWARNING: This test is incompatible with Cray systems\n"
	exit $exit_code
}

if {[test_assoc_enforced]} {
	send_user "\nWARNING: This test will not work when associations are enforced.\n"
	exit $exit_code
}
if {[test_account_storage]} {
	send_user "\nWARNING: This test will not work when invalid account names are subject to change.\n"
	exit $exit_code
}

global env
set env(SALLOC_ACCOUNT) $job_acct

#
# Build input script file
#
make_bash_script $file_in "$bin_sleep 10"

#
# Spawn a srun batch job that just sleeps for a while
#
set timeout $max_job_delay
set sbatch_pid [spawn $sbatch --output=/dev/null --error=/dev/null --account=$job_acct -t1 $file_in]
expect {
	 -re "Submitted batch job ($number)" {
		set job_id1 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		slow_kill $sbatch_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id1 == 0} {
	send_user "\nFAILURE: batch submit failure\n"
	exit 1
}

#
# Submit a second job that gets account from SALLOC_ACCOUNT environment variable
#
set match_acct  0
set salloc_pid [spawn $salloc --dependency=afterany:$job_id1 $srun $scontrol show job $job_id1]
expect {
	-re "Granted job allocation ($number)" {
		set job_id2 $expect_out(1,string)
		exp_continue
	}
	-re "Account=$job_acct_lc" {
		set match_acct 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: salloc not responding\n"
		if {$job_id2 != 0} {
			cancel_job $job_id
		}
		slow_kill [expr 0 - $salloc_pid]
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$match_acct == 0} {
	send_user "\nFAILURE: Account information not processed\n"
	set exit_code 1
}

#
# Confirm  second job's as account name
#
set match_acct 0
spawn $scontrol show job $job_id2
expect {
	-re "Account=$job_acct_lc" {
		set match_acct 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scontrol not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$match_acct == 0} {
	send_user "\nFAILURE: Account information not processed\n"
	set exit_code 1
}

if {$exit_code == 0} {
	exec $bin_rm -f $file_in
	send_user "\nSUCCESS\n"
}
exit $exit_code
