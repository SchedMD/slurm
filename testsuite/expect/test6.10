#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Test scancel user and partition filters, delete all remaining jobs 
#          (--partition and --user options).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
# Note:    This script generates and then deletes files in the working directory 
#          named test6.10.input
############################################################################
# Copyright (C) 2002 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
# 
# This file is part of SLURM, a resource management program.
# For details, see <https://computing.llnl.gov/linux/slurm/>.
# Please also read the included file: DISCLAIMER.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set test_id       "6.10"
set default_part  ""
set exit_code     0
set file_in       "test$test_id.input"
set job_id        0

print_header $test_id

#
# Execute 'id' to determine my login name
#

set got_login 0
spawn $bin_id -un
expect {
        -re "($alpha_numeric_under)" {
		set my_login $expect_out(1,string)
		set got_login 1
		exp_continue
	}
	eof {
		wait
	}
}

if { $got_login == 0 } {
	send_user "\nFAILURE: Unable to get login name\n"
	exit 1
}

#
# Build input script file
#
make_bash_script $file_in "$srun $bin_sleep $max_job_delay"

#
# Spawn a sbatch job
#
set sbatch_pid [spawn $sbatch --output=/dev/null --error=/dev/null --hold -t5 $file_in]
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		slow_kill $sbatch_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: batch submit failure\n"
	exit 1
}
exec $bin_rm -f $file_in

#
# Identify this job's parition
#
set matches  0
spawn $scancel --interactive $job_id
expect {
	-re "Cancel job_id=$job_id .* partition=($alpha_numeric_under) .*\? " {
		set default_part $expect_out(1,string)
		send "n\r"
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scancel not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}

#
# Kill all jobs by parition and user
#
spawn $scancel --interactive --user=$my_login --partition=NO_VAL
expect {
	-re "Cancel job_id=$job_id .* partition=NO_VAL .*\? " {
		send "n\r"
		exp_continue
	}
	-re "Cancel job_id=$job_id .* partition= .*\? " {
		send "n\r"
		send_user "\nFAILURE: scancel partition filter failure\n"
		set exit_code 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scancel not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}

spawn $scancel --interactive --user=$my_login --partition=$default_part
expect {
	-re "Cancel job_id=.* partition=$default_part .*\? " {
		send "n\r"
		exp_continue
	}
	-re "Cancel job_id=.* partition= .*\? " {
		send "n\r"
		send_user "\nFAILURE: scancel partition filter failure\n"
		set exit_code 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scancel not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}

spawn $scancel --verbose --user=$my_login
expect {
	timeout {
		send_user "\nFAILURE: scancel not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}

set matches 0
spawn $scancel --verbose --user=$my_login
expect {
	-re "Signal 9 to job" {
		incr matches
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scancel not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}

if {$matches != 0} {
	send_user "\nFAILURE: scancel user filter failed\n"
	set exit_code 1
}

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code
