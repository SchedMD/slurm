#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Test of LAM-MPI functionality
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "WARNING: ..." with an explanation of why the test can't be made, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2008 Lawrence Livermore National Security.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
# 
# This file is part of SLURM, a resource management program.
# For details, see <https://computing.llnl.gov/linux/slurm/>.
# Please also read the included file: DISCLAIMER.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set test_id     "1.93"
set exit_code   0
set file_in     "test$test_id.input"
set prompt      "PROMPT: "

print_header $test_id

if {[test_front_end] != 0} {
	send_user "\nWARNING: This test is incompatible with front-end systems\n"
	exit 0
}

#
# Build input script file
#
make_bash_script $file_in "
  export PS1=\"$prompt\"
  $bin_bash --norc
"

#
# Submit a 2 node job
#
set timeout $max_job_delay
set salloc_pid [spawn $salloc -t1 -n2 ./$file_in]
expect {
	-re "More processors requested than permitted" {
		send_user "\nWARNING: can't test srun task distribution\n"
		exec $bin_rm -f $file_in
		exit $exit_code
	}
	-re "Unable to contact" {
		send_user "\nFAILURE: slurm appears to be down\n"
		exit 1
	}
	"$prompt" {
		send_user "Job initiated\n"
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		slow_kill $salloc_pid
		exit 1
	}
	eof {
		wait
		send_user "\nFAILURE: srun terminated\n"
		exit 1
	}
}
exec $bin_rm -f $file_in

#
# Get node names
#
set host_0      ""
set host_1      ""
send "$srun -l --mpi=lam $bin_printenv SLURMD_NODENAME\n"
expect {
	-re "($number): ($alpha_numeric_under)" {
		set host_inx $expect_out(1,string)
		if {$host_inx == 0} {
			set host_0 $expect_out(2,string)
		} 
		if {$host_inx == 1} {
			set host_1 $expect_out(2,string)
		}
		exp_continue
	}
	-re "Unable to contact" {
		send_user "\nFAILURE: slurm appears to be down\n"
		exit 1
	}
	"$prompt" {
		send_user "srun completed\n\n"
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		slow_kill $salloc_pid
		exit 1
	}
	eof {
		send_user "\nFAILURE: srun EOF\n"
		exit 1
	}
}

#
# Verify node count
#
if {[string compare $host_0 ""] == 0} {
	send_user "\nFAILURE: no response from task zero\n"
	set exit_code   1
}
if {[string compare $host_0 $host_1] == 0} {
	send_user "\nFAILURE: mulitple tasks ran on a single node with --mpi=lam option\n"
	set exit_code   1
}

#
# Post-processing
#
send "exit\n"
expect {
	-re "error" {
		send_user "\nFAILURE: Some error occured\n"
		set exit_code   1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		slow_kill $salloc_pid
		exit 1
	}
	eof {
		wait
	}
}

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code
