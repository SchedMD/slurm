#!/usr/bin/env expect
############################################################################
# Purpose: Test of Slurm functionality
#          Validate scontrol update command for job steps.
# Copyright (C) 2010 Lawrence Livermore National Security.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
#
# This file is part of Slurm, a resource management program.
# For details, see <https://slurm.schedmd.com/>.
# Please also read the included file: DISCLAIMER.
#
# Slurm is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# Slurm is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License along
# with Slurm; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set exit_code     0
set file_in1      "test$test_id.input1"
set file_in2      "test$test_id.input2"
set file_out      "test$test_id.output"
set job_id        0
set step_id       0
set orig_time     0
set new_time      0

proc cleanup {} {
	global bin_rm file_in1 file_in2 file_out

	exec $bin_rm -f $file_in1 $file_in2 $file_out
}

#
# Build input batch file
#
exec $bin_rm -f $file_in1 $file_in2 $file_out
make_bash_script $file_in1 "
  $bin_echo JOB_BEGIN
  $srun -t5 ./$file_in2
  $bin_echo JOB_FINISH
"
#
# Build step file
# The second sleep is because with some proctrack plugins, the bash shell may
# be terminated slighly later than the first sleep process
#
make_bash_script $file_in2 "
  $bin_echo STEP_BEGIN
  $bin_sleep 120
  $bin_sleep 1
  $bin_echo STEP_FINISH
"

#
# Submit a job so we have something to work with
#
set srun_pid [spawn $sbatch --output=$file_out -t3 $file_in1]
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		slow_kill $srun_pid
		fail "srun not responding"
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	fail "srun failed to initiate job"
}
#
# Wait for job _and_ step to start running
#
if {[wait_for_job $job_id "RUNNING"] != 0} {
    fail "Waiting for job to start running"
}
sleep 5

#
# Validate that step's original time limit
#
spawn  $scontrol show step $job_id.$step_id
expect {
	-re "TimeLimit=00:($number):00" {
		set orig_time $expect_out(1,string)
		exp_continue
	}
	timeout {
		fail "scontrol not responding"
	}
	eof {
		wait
	}
}
if {$orig_time != 5} {
    log_error "Failed to set step initial time limit"
	set exit_code 1
}

#
# Change that step's time limit
#
spawn $scontrol update StepId=$job_id TimeLimit=1
expect {
	timeout {
		fail "scontrol not responding"
	}
	eof {
		wait
	}
}

#
# Validate that step's new time limit
#
spawn  $scontrol show step $job_id.$step_id
expect {
	-re "TimeLimit=00:($number):00" {
		set orig_time $expect_out(1,string)
		exp_continue
	}
	timeout {
		fail "scontrol not responding"
	}
	eof {
		wait
	}
}
if {$orig_time != 1} {
    log_error "Failed to update step time limit"
	set exit_code 1
}

#
# Wait for step to exit and check output file
# Look for message generated before and after srun (job step) time limit
#
sleep 60
if {[wait_for_job $job_id "DONE"] != 0} {
    fail "Waiting for job to terminate"
}
if {[wait_for_file $file_out] != 0} {
	fail "Job output file not found"
}

spawn $bin_cat $file_out
expect {
	-re "STEP_FINISH" {
		log_error "Step time limit not enforced"
		set exit_code 1
	}
	eof {
		wait
	}
}

if {$exit_code != 0} {
	fail "Test failed due to previous errors (\$exit_code = $exit_code)"
}
