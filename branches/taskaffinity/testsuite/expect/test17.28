#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Tests #SLURM entry functionality in a batch script.
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2005-2007 The Regents of the University of California.
# Copyright (C) 2008 Lawrence Livermore National Security.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Danny Auble <da@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
# 
# This file is part of SLURM, a resource management program.
# For details, see <https://computing.llnl.gov/linux/slurm/>.
# Please also read the included file: DISCLAIMER.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set test_id     "17.28"
set exit_code   0
set file_in     "test$test_id.input"
set file_out    "test$test_id.output"
set job_name    "TEST_NAME"
set delay       1

print_header $test_id

make_bash_script $file_in "
#SBATCH --job-name=$job_name
$bin_sleep $delay
"

set timeout $max_job_delay
set job_id 0
spawn $sbatch -o $file_out -vvvv $file_in
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		set exit_code 1
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: sbatch submit failure\n"
	exit 1
}
set matches 0
spawn $scontrol show job $job_id
expect {
	-re "Name=$job_name" {
		incr matches
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scontrol not responding\n"
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}
if {$matches != 1} {
	send_user "\nFAILURE: did not set job name from batch script\n"
	set exit_code 1
}
cancel_job $job_id

#
# Build input script file
# NOTE: The initial sleep is so that all of the submissions have time 
#   to occur before contending with a multitude of job step creations.
#   This is especially important on very slow systems (e.g. AIX).
#
make_bash_script $file_in "
#SBATCH -N1000000k
$bin_sleep $delay
"
set job_id  0
set matches 0
spawn $sbatch -o $file_out $file_in
expect {
	-re "More processors requested than permitted" {
		send_user "This error was expected, no worries\n\n"
		incr matches
		exp_continue
	}
	-re "Node count specification invalid" {
		send_user "This error was expected, no worries\n\n"
		incr matches
		exp_continue
	}	
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}
if {$matches != 1} {
	send_user "\nFAILURE: sbatch didn't read the correct options from batch file\n"
	set exit_code 1
}
if {$job_id != 0} {
	send_user "\nFAILURE: sbatch didn't reject job with invalid size\n"
	cancel_job $job_id
	set exit_code 1
}

make_bash_script $file_in "
#SBATCH -N650000
$bin_sleep $delay
"

set job_id  0
spawn $sbatch -N1 -o $file_out $file_in
expect {
	-re "Node count specification invalid" {
		send_user "\nFAILURE: sbatch read from the batch file options"
		send_user "over writing the commandline options\n"
		set exit_code 1
		exp_continue
	}
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: sbatch didn't reject job with invalid size\n"
	cancel_job $job_id
	set exit_code 1
}

#
# Post-processing
#
if {$exit_code == 0} {
	exec $bin_rm -f $file_in $file_out
	send_user "\nSUCCESS\n"
}
exit $exit_code
