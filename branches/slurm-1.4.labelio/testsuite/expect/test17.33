#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Test of sbatch --open-mode (truncate or append) option.
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2002-2007 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# CODE-OCEC-09-009. All rights reserved.
# 
# This file is part of SLURM, a resource management program.
# For details, see <https://computing.llnl.gov/linux/slurm/>.
# Please also read the included file: DISCLAIMER.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA.
############################################################################
source ./globals

set test_id     "17.33"
set file_in     "test$test_id.input"
set file_out    "test$test_id.output"
set file_err    "test$test_id.error"
set exit_code            0
set login_grp_info       ""
set got_job_grps         0
set got_login_grps       0
set got_sleep_err        0

print_header $test_id

#
# Delete left-over stdin/out/err files
# Build stdin file
#
exec $bin_rm -f $file_in $file_out $file_err
make_bash_script $file_in "
  $bin_id
  $bin_sleep aaa
  exit 0"
exec echo "$bin_echo INITIAL_VALUE" >$file_err
exec echo "$bin_echo INITIAL_VALUE" >$file_out

#
# Get user id and group id for comparison with stdout
#
spawn $bin_id
expect {
	-re "(uid=$number)" {
		set login_grp_info $expect_out(1,string)
		set got_login_grps 1
		exp_continue
	}
	eof {
		wait
	}
}

#
# Spawn a shell via srun that uses stdin/out/err in truncate mode
# and confirm their contents
#
set job_id 0
set srun_pid [spawn $sbatch --output=$file_out --error=$file_err --open-mode=t -t1 $file_in]
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: sbatch failed to submit job\n"
	exit 1
}
if {[wait_for_job $job_id "DONE"] != 0} {
	send_user "\nFAILURE: waiting for job to complete\n"
	cancel_job $job_id
	exit 1
}

if {[wait_for_file $file_out] == 0} {
	spawn $bin_cat $file_out
	expect {
		-re "INITIAL_VALUE" {
			send_user "\nFAILURE: stdout file not truncated\n"
			set exit_code 1
		}
		-re "$login_grp_info" {
			incr got_job_grps
			exp_continue
		}
		eof {
			wait
		}
	}
}

if {$got_login_grps == 0} {
	send_user "\nFAILURE: Unable to get user and group ID info\n"
	set exit_code 1
}
if {$got_job_grps != 1} {
	send_user "\nFAILURE: User and group ID info missing from stdout\n"
	set exit_code 1
}

#
# Check for sleep input specification error in stderr
#
if {[wait_for_file $file_err] == 0} {
	spawn $bin_cat $file_err
	expect {
		-re "INITIAL_VALUE" {
			send_user "\nFAILURE: stderr file not truncated\n"
			set exit_code 1
		}
		-re "$sleep_error_message" {
			send_user "\nNo worries, this error is expected\n"
			incr got_sleep_err
			exp_continue
		}
		-re "Specify time as a positive integer.*\n" {
			incr got_sleep_err
			exp_continue
		}
		eof {
			wait
		}
	}
}
if {$got_sleep_err != 1} {
	send_user "\nFAILURE: Unexpected stderr contents\n"
	set exit_code 1
}

if {$exit_code != 0} {
	exit $exit_code
}


#
# Spawn a shell via srun that uses stdin/out/err in append mode
# and confirm their contents
#
set job_id 0
set srun_pid [spawn $sbatch --output=$file_out --error=$file_err --open-mode=a -t1 $file_in]
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	eof {
		wait
	}
}
if {$job_id == 0} {
	send_user "\nFAILURE: sbatch failed to submit job\n"
	exit 1
}
if {[wait_for_job $job_id "DONE"] != 0} {
	send_user "\nFAILURE: waiting for job to complete\n"
	cancel_job $job_id
	exit 1
}

set got_job_grps 0
if {[wait_for_file $file_out] == 0} {
	spawn $bin_cat $file_out
	expect {
		-re "$login_grp_info" {
			incr got_job_grps
			exp_continue
		}
		eof {
			wait
		}
	}
}
if {$got_job_grps != 2} {
	send_user "\nFAILURE: User and group ID info missing from stdout\n"
	set exit_code 1
}

#
# Check for sleep input specification error in stderr
#
set got_sleep_err 0
if {[wait_for_file $file_err] == 0} {
	spawn $bin_cat $file_err
	expect {
		-re "$sleep_error_message" {
			send_user "\nNo worries, this error is expected\n"
			incr got_sleep_err
			exp_continue
		}
		-re "Specify time as a positive integer.*\n" {
			incr got_sleep_err
			exp_continue
		}
		eof {
			wait
		}
	}
}
if {$got_sleep_err != 2} {
	send_user "\nFAILURE: Unexpected stderr contents\n"
	set exit_code 1
}


if {$exit_code == 0} {
	exec $bin_rm -f $file_in $file_out $file_err
	send_user "\nSUCCESS\n"
}
exit $exit_code
