#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Confirm that as sbatch job executes as the appropriate user and 
#          group.
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# LLNL-CODE-402394.
# 
# This file is part of SLURM, a resource management program.
# For details, see <http://www.llnl.gov/linux/slurm/>.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set test_id              "17.4"
set exit_code            0
set file_in              "test$test_id.input"
set file_out             "test$test_id.output"
set login_grp_info       ""
set job_id               0
set job_grp_info         ""
set got_job_grps         0
set got_login_grps       0

print_header $test_id

#
# Execute 'id' to determine my user and group ID...
#

spawn $bin_id
expect {
        -re "(uid=.*\n)" {
		set login_grp_info $expect_out(1,string)
		set got_login_grps 1
		exp_continue
	}
	eof {
		wait
	}
}

if {$got_login_grps == 0} {
	send_user "\nFAILURE: Unable to get user and group ID info\n"
	exit 1
}

#
# Submit a slurm job that will execute 'id'
#
exec $bin_rm -f $file_in $file_out
make_bash_script $file_in "
  $bin_id
"

spawn $sbatch -N1 -t1 --output=$file_out $file_in  
expect {
	-re "Submitted batch job ($number)" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}
if { $job_id == 0 } {
	send_user "\nFAILURE: failed to submit job\n"
	exit 1
}

#
# Wait for job to complete
#
if {[wait_for_job $job_id "DONE"] != 0} {
	send_user "\nFAILURE: waiting for job to complete\n"
	set exit_code 1
}

#
# Check for desired output
#
if {[wait_for_file $file_out] == 0} {
	spawn $bin_cat $file_out
	expect {
		-re "(uid=.*\n)" {
			set job_grp_info $expect_out(1,string)
			set got_job_grps 1
			exp_continue
		}
		eof {
			wait
		}
	}
}

if {$got_job_grps == 0} {
	send_user "\nFAILURE: Did not get user info from slurm job\n"
	exit 1
}
  
#
# Confirm the user id and group id in the slurm job matches that  
# of the local 'id' execution.
#
if {[string compare $login_grp_info $job_grp_info] != 0} {
	send_user "\nFAILURE: Login and sbatch user info mismatch\n"
	set exit_code 1
}

if {$exit_code == 0} {
	exec $bin_rm -f $file_in $file_out
	send_user "\nSUCCESS\n"
}
exit $exit_code
