#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Confirm that job time limit function works (-t option).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2002 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# UCRL-CODE-2002-040.
# 
# This file is part of SLURM, a resource management program.
# For details, see <http://www.llnl.gov/linux/slurm/>.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set test_id      "1.7"
set exit_code    0
set sleep_time   180

print_header $test_id

#
# Execute a couple of three minute jobs; one with a one minute time  
# limit and the other with a four minute time limit. Confirm jobs 
# are terminated on a timeout as required. Note that SLURM time 
# limit enforcement has a resolution of about one minute. 
#
# Ideally the job gets a "job exceeded timelimit" followed by a 
# "Terminated" message, but if the timing is bad only the "Terminated" 
# message gets sent. This is due to srun recognizing job termination 
# prior to the message from slurmd being processed.
#
set timeout [expr $max_job_delay + $sleep_time]
set timed_out 0
spawn $srun -t1 $bin_sleep $sleep_time
expect {
	-re "job exceeded timelimit" {
		set timed_out 1
		exp_continue
	}
	-re "Terminated" {
		set timed_out 1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
		wait
	}
}
if {$timed_out == 1} {
	send_user "Early termination is expected, no worries.\n"
} else {
	send_user "\nFAILURE: time limit not enforced\n"
	set exit_code 1
}

set completions 0
spawn $srun -t4 $bin_sleep $sleep_time
expect {
	-re "job exceeded timelimit" {
		set completions -1
		exp_continue
	}
	-re "Terminated" {
		set completions -1
		exp_continue
	}
	-re "error" {
		set completions -1
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
		wait
		incr completions
	}
}
if {$completions != 1} {
	send_user "\nFAILURE: job failed to complete properly\n"
	set exit_code 1
}

if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code
