#!/usr/bin/expect
#
# Purpose: Test of SLURM functionality
#          Validate scontrol update command for nodes.
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
#
source ./globals

set test_id       "3.4"
set authorized    1
set exit_code     0
set file_in       "test$test_id.input"
set job_id        0
set new_prio      1
set read_priority 0

print_header $test_id

#
# Delete left-over input script
# Build input script file
#
exec $bin_rm -f $file_in
exec echo "#!$bin_bash"          >$file_in
exec echo "$srun $bin_sleep 60" >>$file_in
exec $bin_chmod 700 $file_in

#
# Submit a job so we have something to work with
#
spawn $srun --batch --output=/dev/null --error=/dev/null -t1 $file_in
expect {
	-re "jobid ($number) submitted" {
		set job_id $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		kill_srun
		set exit_code 1
		exp_continue
	}
	eof {
	}
}
exec $bin_rm -f $file_in
if {$job_id == 0} {
	send_user "\nFAILURE: srun failed to initiate job\n"
	exit 1
}

#
# Record that job's state
#
spawn  $scontrol show job $job_id
expect {

	timeout {
		send_user "\nFAILURE: scontrol not responding\n"
		set exit_code 1
	}
	eof {
	}
}

#
# Change that job's priority
#
spawn $scontrol update JobId=$job_id Priority=$new_prio 
expect {
	-re "slurm_update error: ($alpha_numeric) ($alpha_numeric)" {
		set access_err 0
		set err_msg1 $expect_out(1,string)
		set err_msg2 $expect_out(2,string)
		if {[string compare $err_msg1 "Access"]} {
			set access_err 1
		}
		if {[string compare $err_msg2 "denied"]} {
			set access_err 1
		}
		if {$access_err == 1} {
			send_user "\nFAILURE: scontrol update error\n"
			set exit_code 1
		} else {
			set authorized 0
		}
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scontrol not responding\n"
		set exit_code 1
	}
	eof {
	}
}

#
# Validate that job's new priority
#
spawn  $scontrol show job $job_id
expect {
	-re "Priority=($number)" {
		set read_priority $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: scontrol not responding\n"
		set exit_code 1
	}
	eof {
	}
}
if {$new_prio != $read_priority} {
	send_user "\nFAILURE: scontrol priority change error\n"
	set exit_code 1
}

cancel_job $job_id
if {$exit_code == 0} {
	send_user "\nSUCCESS\n"
}
exit $exit_code

