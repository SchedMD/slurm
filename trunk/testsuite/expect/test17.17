#!/usr/bin/expect
############################################################################
# Purpose: Test of SLURM functionality
#          Confirm that node sharing flags are respected  (--nodelist and 
#          --share options).
#
# Output:  "TEST: #.#" followed by "SUCCESS" if test was successful, OR
#          "WARNING: ..." with an explanation of why the test can't be made, OR
#          "FAILURE: ..." otherwise with an explanation of the failure, OR
#          anything else indicates a failure mode that must be investigated.
############################################################################
# Copyright (C) 2002-2006 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory (cf, DISCLAIMER).
# Written by Morris Jette <jette1@llnl.gov>
# LLNL-CODE-402394.
# 
# This file is part of SLURM, a resource management program.
# For details, see <http://www.llnl.gov/linux/slurm/>.
#  
# SLURM is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
# 
# SLURM is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along
# with SLURM; if not, write to the Free Software Foundation, Inc.,
# 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA.
############################################################################
source ./globals

set test_id      "17.17"
set exit_code    0
set file_err     "test$test_id.error"
set file_in      "test$test_id.input"
set file_out     "test$test_id.output"
set job_id1      0
set host_name    ""
set nodelist_name ""

print_header $test_id

#
# Submit a job and get the node's NodeName from the nodelist
#
set timeout $max_job_delay
set srun_pid [spawn $srun -v -N1 -l -t1 $bin_printenv SLURMD_NODENAME]
expect {
	-re "on host ($alpha_numeric_under)," {
		set nodelist_name $expect_out(1,string)
		exp_continue
	}
	-re "0: ($alpha_numeric_under)" {
		set host_name $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		slow_kill $srun_pid
		set exit_code 1
	}
	eof {
		wait
	}
}
if {[string compare $nodelist_name ""] == 0} {
	send_user "\nFAILURE: Did not get hostname of task 0\n"
	exit 1
}
if {[test_front_end] != 0} {
	send_user "\nWARNING: Additional testing is incompatable with front-end systems\n"
	exit $exit_code
}

#
# Delete left-over input script
# Build input script file
#
exec $bin_rm -f $file_in
make_bash_script $file_in "$srun $bin_sleep 5"

#
# Submit two jobs to the same node, one with no sharing, the other 
# with sharing permitted. Insure the first job completes before the 
# second job is started.
#
set sbatch_pid [spawn $sbatch -N1 --exclusive --nodelist=$nodelist_name -t1 --output=$file_out --error=$file_err  $file_in]
expect {
	-re "Submitted batch job ($number)" {
		set job_id1 $expect_out(1,string)
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: sbatch not responding\n"
		slow_kill $sbatch_pid
		exit 1
	}
	eof {
		wait
	}
}
if {$job_id1 == 0} {
	send_user "\nFAILURE: sbatch failed to report jobid\n"
	exit 1
}

set partition "dummy"
set waited 1
set timeout [expr $max_job_delay + 5]
set srun_pid [spawn $srun -N1 --nodelist=$nodelist_name -t1 --share $scontrol -o show job $job_id1]
expect {
	-re "Partition=($alpha_numeric_under)" {
		set partition $expect_out(1,string)
		exp_continue
	}
	-re "JobState=RUN" {
		set waited 0 
		exp_continue
	}
	timeout {
		send_user "\nFAILURE: srun not responding\n"
		slow_kill $srun_pid
		cancel_job $job_id1
		exit 1
	}
	eof {
		wait
	}
}
if {$waited == 0} {
	spawn $scontrol show partition
	expect {
		-re "Shared=FORCE" {
			send_user "\nWARNING: Test incompatable with Shared=FORCE\n"
			set waited 1
			exp_continue
		}
		eof {
			wait
		}
	}
}
if {$waited == 0} {
	send_user "\nFAILURE: srun failed to wait for non-sharing job to complete\n"
	set exit_code 1
}

cancel_job $job_id1
if {$exit_code == 0} {
	exec $bin_rm -f $file_err $file_in $file_out
	send_user "\nSUCCESS\n"
}
exit $exit_code
